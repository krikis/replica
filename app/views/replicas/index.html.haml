
:coffeescript
  $(document).ready ->
    debug = (string, color="black") ->
      paragraph = $("<p>\#{string}<\p>")
      paragraph.appendTo("#debug")
      paragraph.css("color", color)
      $("#debug").scrollTo(paragraph, 50)
    window.debug = debug

    # set initial reconnection timeout to 2 seconds
    window.prev_reconnect_on = 1
    window.reconnect_on = 2
    @initSocket = ->
      Socket = (if "MozWebSocket" of window then MozWebSocket else WebSocket)
      localStorage._id = "#{session[:key]}"
      localStorage._index = JSON.stringify []
      web_socket = new Socket("ws://localhost:8080/")
      window.web_socket = web_socket

      web_socket.onopen = ->
        # reset reconnection timeouts
        window.prev_reconnect_on = 1
        window.reconnect_on = 2
        debug "connected..."
        window.web_socket.send JSON.stringify(
          id: localStorage._id
          subscribe: true
        )

      web_socket.onmessage = (evt) ->
        # fetch data from localstorage
        id = localStorage._id
        index = JSON.parse(localStorage._index)
        data = JSON.parse(evt.data)
        if data.index?                                 # set the initial index
          data.index[data.index.length] = id unless id in data.index
          index = data.index
        else if data.subscribe? and data.id != id      # handle subscription
          index[index.length] = data.id
        else if data.unsubscribe and data.id != id     # handle unsubscription
          index.delete data.id
        else if data.disable_clear
          $(".clear").addClass("disabled")
        else if data.clear
          document.clear_data()
        else if data.id != id
          document.store_data(data)
          return
        # finally
        # save index updates to localstorage
        localStorage._index = JSON.stringify(index)
        debug evt.data, "Gainsboro"
        debug localStorage._index, "Gainsboro"

      web_socket.onclose = ->
        localStorage._index = JSON.stringify []
        debug "socket closed"
        debug localStorage._index, "Gainsboro"
        unless window.offline?
          debug "reconnecting in \#{window.reconnect_on} seconds...", "Gainsboro"
          setTimeout (->
            document.initSocket()
          ), window.reconnect_on * 1000
          # increase the timeout fibonacciwise in order to decrease the network load
          tmp = window.reconnect_on
          window.reconnect_on = window.reconnect_on + window.prev_reconnect_on
          window.prev_reconnect_on = tmp

    @store_data = (data) ->
      vector = localStorage._vector
      # The message was already delivered
      if vector[data.id] >= data.vector[data.id]
        debug "Old message", "Crimson"
        return
      # There are messages missing from processes other than the sender
      if (errors = id for id of vector when vector[id] < data.vector.id).length > 1
        debug "Missing messages from others!", "Crimson"
        return
      # There are messages missing from the sender
      if (vector[data.id] < data.vector[data.id] + 1)
        debug "Missing messages from sender!", "Crimson"
        return
      # The received data is corrupt
      if data.crc? and crc32("\#{data.key}|\#{data.value}") != data.crc
        debug "Checksum Error!", "Crimson"
        return
      # Actually store the data
      {key, value, old_value} = data
      local_old_value = localStorage[key]
      local_old_value ?= "#ffffff"
      localStorage[key] = value
      $("<div id='\#{key}' class='color_column'>").appendTo("#data") if $("#data > #\#{key}").length == 0
      color_box = $("<div class='color_box'>")
      color_box.appendTo("#data > #\#{key}")
      color_box.css("border", "1px solid red") if local_old_value != old_value
      color_box.css("background", "-webkit-gradient(linear, left top, left bottom, from(\#{old_value}), to(\#{value}))")
      $("#data > #\#{key}").scrollTo(color_box, 50)
      debug "\#{key}: \#{old_value} -> \#{value}", "Gainsboro"

    @clear_data = ->
      for key of localStorage when key[0] != '_'
        localStorage.removeItem key
      $(".color_column").html("")

    if Modernizr.localstorage and Modernizr.websockets
      document.initSocket()                              # initialize a web socket

#startbutton.button.start Start
#clearbutton.button.clear Clear
#offbutton.button.offline Offline
#logbutton.button.log Show Log

%div.container
  #data
    #key_00.color_column
    #key_01.color_column
    #key_02.color_column
    #key_03.color_column
    #key_04.color_column
    #key_05.color_column
    #key_06.color_column
    #key_07.color_column
    #key_08.color_column
    #key_09.color_column
    #key_10.color_column
    #key_11.color_column
    #key_12.color_column
    #key_13.color_column
    #key_14.color_column
    #key_15.color_column
    #key_16.color_column
    #key_17.color_column
    #key_18.color_column
    #key_19.color_column
#debug

:coffeescript
  $(document).ready ->
    # fetch data from localstorage
    id = localStorage._id
    index = JSON.parse localStorage._index
    @send_data = ->
      # increment vector timestamp for this process
      count = ++localStorage._vector[localStorage._id]
      # generate data for localstorage
      number = Math.floor(Math.random() * 20).toString()
      number = "0" + number until number.length == 2
      key = "key_" + number
      value = window.random_color()
      old_value = localStorage[key]
      old_value ?= "#ffffff"
      data =
        id: id
        key: key
        value: value
        old_value: old_value
        crc: crc32("\#{key}|\#{value}")
        vector: localStorage._vector
      # push the message in the sent queue for later reference
      localStorage._send_queue[count.toString()] = data
      # send and store the data
      window.web_socket.send JSON.stringify(data)
      document.store_data(data)

    if Modernizr.localstorage and Modernizr.websockets
      $("#startbutton").click ->
        unless window.interval_id?
          window.web_socket.send JSON.stringify(id: id, disable_clear: true)
          window.interval_id = window.setInterval document.send_data, 1000
          $(this).html("Stop")
                 .removeClass("start")
                 .addClass("stop")
          $(".clear").addClass("disabled")
        else
          window.clearInterval window.interval_id
          window.interval_id = null
          $(this).html("Start")
                 .removeClass("stop")
                 .addClass("start")
          $(".clear").removeClass("disabled")

      $("#clearbutton").click ->
        unless $(this).hasClass("disabled")
          window.web_socket.send JSON.stringify(
            id: id
            clear: true
          )

      $("#offbutton").click ->
        if $(this).html() == "Offline"
          $(this).html("Online")
                 .removeClass("offline")
                 .addClass("online")
          window.offline = true
          window.web_socket.close()
        else
          $(this).html("Offline")
                 .removeClass("online")
                 .addClass("offline")
          window.offline = null
          document.initSocket()

      $("#logbutton").click ->
        if $(this).html() == "Show Log"
          $(this).html("Hide Log")
          $("#debug").show()
        else
          $(this).html("Show Log")
          $("#debug").hide()
